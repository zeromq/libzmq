# CMake build script for ZeroMQ

cmake_minimum_required(VERSION 2.8)
project(ZeroMQ)

option(WITH_OPENPGM "Build with support for OpenPGM" OFF)

if(WIN32)
    option(WITH_TWEETNACL "Build with tweetnacl" OFF)
else()
    option(WITH_TWEETNACL "Build with tweetnacl" ON)
endif()

if(WITH_TWEETNACL)
    add_definitions(-DHAVE_TWEETNACL -DHAVE_LIBSODIUM)
    include_directories(tweetnacl/contrib/randombytes
                        tweetnacl/src
                        )

    set(TWEETNACL_SOURCES
        tweetnacl/src/tweetnacl.c 
        )
    if(WIN32)
    else()
        list(APPEND TWEETNACL_SOURCES tweetnacl/contrib/randombytes/devurandom.c)
    endif()
else()
    find_library(SODIUM_FOUND sodium)
endif()

set(POLLER "" CACHE STRING "Choose polling system. valid values are
                            kqueue, epoll, devpoll, poll or select [default=autodetect]")

include(CheckFunctionExists)
include(CheckTypeSize)
if(POLLER STREQUAL "")
    set(CMAKE_REQUIRED_INCLUDES sys/event.h)
    check_function_exists(kqueue HAVE_KQUEUE)
    set(CMAKE_REQUIRED_INCLUDES )
    if(HAVE_KQUEUE)
        set(POLLER "kqueue")
    else()
        set(CMAKE_REQUIRED_INCLUDES sys/epoll.h)
        check_function_exists(epoll_create HAVE_EPOLL)
        set(CMAKE_REQUIRED_INCLUDES )
        if(HAVE_EPOLL)
            set(POLLER "epoll")
        else()
            set(CMAKE_REQUIRED_INCLUDES sys/devpoll.h)
            check_type_size("struct pollfd" DEVPOLL)
            set(CMAKE_REQUIRED_INCLUDES )
            if(HAVE_DEVPOLL)
                set(POLLER "devpoll")
            else()
                set(CMAKE_REQUIRED_INCLUDES poll.h)
                check_function_exists(poll HAVE_POLL)
                set(CMAKE_REQUIRED_INCLUDES )
                if(HAVE_POLL)
                    set(POLLER "poll")
                else()
                    if(WIN32)
                        set(CMAKE_REQUIRED_INCLUDES winsock2.h)
                        set(HAVE_SELECT 1)
                    else()
                        set(CMAKE_REQUIRED_INCLUDES sys/select.h)
                        check_function_exists(select HAVE_SELECT)
                    endif()
                    set(CMAKE_REQUIRED_INCLUDES )
                    if(HAVE_SELECT)
                        set(POLLER "select")
                    else()
                        message(FATAL_ERROR
                            "Could not autodetect polling method")
                    endif()
                endif()
            endif()
        endif()
    endif()
endif()

if(     NOT POLLER STREQUAL "kqueue"
    AND NOT POLLER STREQUAL "epoll"
    AND NOT POLLER STREQUAL "devpoll"
    AND NOT POLLER STREQUAL "poll"
    AND NOT POLLER STREQUAL "select")
  message(FATAL_ERROR "Invalid polling method")
endif()

if(NOT ${POLLER} STREQUAL "")
    string(TOUPPER ${POLLER} UPPER_POLLER)
    set(ZMQ_USE_${UPPER_POLLER} 1)
endif()

set(ZMQ_CMAKE_MODULES_DIR ${CMAKE_CURRENT_SOURCE_DIR}/builds/cmake/Modules)
list(APPEND CMAKE_MODULE_PATH ${ZMQ_CMAKE_MODULES_DIR})

include(TestZMQVersion)
include(ZMQSourceRunChecks)
include(CheckCCompilerFlag)
include(CheckCXXCompilerFlag)
include(CheckCSourceCompiles)
include(CheckCSourceRuns)
include(CMakeDependentOption)
include(CheckCXXSymbolExists)

# MACRO to find include file
macro(zmq_find_include include_name var_result)
    message(STATUS "Looking for header ${include_name}")
    find_file(file_result NAMES "${include_name}" )
#    message(STATUS "Find File result - ${file_result}")    
    string(FIND "${file_result}" "NOTFOUND" pos)
    if (pos EQUAL -1)
        #found include file
        message(STATUS "Looking for header ${include_name} - found")
        set("${var_result}" "${file_result}")
    else()
        # did not find include
        message(STATUS "Looking for header ${include_name} - not found")
        set("${var_result}" "")
    endif()
    #clear the find result
    unset(file_result CACHE)
endmacro()

# MACRO to find a library
macro(zmq_find_library library_name var_result)
    message(STATUS "Looking for library ${library_name}")
    find_library(lib_result NAMES "${library_name}" )
#    message(STATUS "Find Library result - ${lib_result}")    
    string(FIND "${lib_result}" "NOTFOUND" pos)
    if (pos EQUAL -1)
        #found library
        message(STATUS "Looking for library ${library_name} - found")
        set("${var_result}" "${lib_result}")
    else()
        # did not find library
        message(STATUS "Looking for library ${library_name} - not found")
        set("${var_result}" "")
    endif()
    #clear the find result
    unset(lib_result CACHE)
endmacro()

#use find file instead of check_include_files
zmq_find_include("ifaddrs.h" ZMQ_HAVE_IFADDRS)
zmq_find_include("windows.h" ZMQ_HAVE_WINDOWS)
zmq_find_include("sys/uio.h" ZMQ_HAVE_UIO)
zmq_find_include("sys/eventfd.h" ZMQ_HAVE_EVENTFD)

#use find library instead of check library which fails on VS2015
zmq_find_library("ws2_32" HAVE_WS2_32)
zmq_find_library("ws2" HAVE_WS2)
zmq_find_library("rpcrt4" HAVE_RPCRT4) # UuidCreateSequential
zmq_find_library("iphlpapi" HAVE_IPHLAPI) # GetAdaptersAddresses
     
check_cxx_symbol_exists(SO_PEERCRED sys/socket.h ZMQ_HAVE_SO_PEERCRED)
check_cxx_symbol_exists(LOCAL_PEERCRED sys/socket.h ZMQ_HAVE_LOCAL_PEERCRED)

#find_library(RT_LIBRARY rt)
#find_package(Threads)

if(WIN32 AND NOT CYGWIN)
    if(NOT HAVE_WS2_32 AND NOT HAVE_WS2)
        message(FATAL_ERROR "Cannot link to ws2_32 or ws2")
    endif()

    if(NOT HAVE_RPCRT4)
        message(FATAL_ERROR "Cannot link to rpcrt4")
    endif()

    if(NOT HAVE_IPHLAPI)
        message(FATAL_ERROR "Cannot link to iphlapi")
    endif()
endif()

set(CMAKE_REQUIRED_LIBRARIES rt)
check_function_exists(clock_gettime HAVE_CLOCK_GETTIME)
set(CMAKE_REQUIRED_LIBRARIES )

set(CMAKE_REQUIRED_INCLUDES unistd.h)
check_function_exists(fork HAVE_FORK)
set(CMAKE_REQUIRED_INCLUDES )

set(CMAKE_REQUIRED_INCLUDES sys/time.h)
check_function_exists(gethrtime HAVE_GETHRTIME)
set(CMAKE_REQUIRED_INCLUDES )

if(MSVC)
else()
    add_definitions(-D_REENTRANT -D_THREAD_SAFE)
endif()

option(ENABLE_EVENTFD "Enable/disable eventfd" ZMQ_HAVE_EVENTFD)

macro(zmq_check_cxx_flag_prepend flag)
    check_cxx_compiler_flag("${flag}" HAVE_FLAG_${flag})

    if(HAVE_FLAG_${flag})
        set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} ${flag}")
    endif()
endmacro()


if(MSVC)
    zmq_check_cxx_flag_prepend("/W3")
else()
    zmq_check_cxx_flag_prepend("-Wall")
endif()

if(CMAKE_COMPILER_IS_GNUCXX OR CMAKE_CXX_COMPILER_ID MATCHES "Clang")
  zmq_check_cxx_flag_prepend("-Wextra")
endif()

zmq_check_cxx_flag_prepend("-Wno-long-long")
zmq_check_cxx_flag_prepend("-Wno-uninitialized")

option(LIBZMQ_PEDANTIC "" ON)
option(LIBZMQ_WERROR "" OFF)

if(LIBZMQ_PEDANTIC)
  zmq_check_cxx_flag_prepend("-pedantic")

  if(${CMAKE_CXX_COMPILER_ID} MATCHES "Intel")
    zmq_check_cxx_flag_prepend("-strict-ansi")
  endif()
endif()

if(LIBZMQ_WERROR)
  zmq_check_cxx_flag_prepend("-Werror")
  zmq_check_cxx_flag_prepend("-errwarn=%all")
endif()

#-----------------------------------------------------------------------------
zmq_check_sock_cloexec()
zmq_check_so_keepalive()
zmq_check_tcp_keepcnt()
zmq_check_tcp_keepidle()
zmq_check_tcp_keepintvl()
zmq_check_tcp_keepalive()


if(    CMAKE_SYSTEM_NAME MATCHES "Linux"
    OR CMAKE_SYSTEM_NAME MATCHES "GNU/kFreeBSD"
    OR CMAKE_SYSTEM_NAME MATCHES "GNU/Hurd"
    OR CYGWIN)
    add_definitions(-D_GNU_SOURCE)
endif()

if(NOT WITHOUT_ASCIIDOC)
    set(CMAKE_PYTHON_VERSION 3.4 3.3 3.2 3.1 3.0 2.7 2.6 2.5 2.4)
    find_package(PythonInterp)
    find_package(AsciiDoc)
else()
    set(PYTHON_FOUND OFF)
    set(ASCIIDOC_FOUND OFF)
endif()

cmake_dependent_option(WITH_DOC "Build Reference Guide documentation(requires DocBook)" ON
                       "PYTHON_FOUND;ASCIIDOC_FOUND" OFF)

if(MSVC)
    if(WITH_OPENPGM)
        #   set(OPENPGM_ROOT "" CACHE PATH "Location of OpenPGM")
        set(OPENPGM_VERSION_MAJOR 5)
        set(OPENPGM_VERSION_MINOR 2)
        set(OPENPGM_VERSION_MICRO 122)
        if(CMAKE_CL_64)
            find_path(OPENPGM_ROOT include/pgm/pgm.h
                      PATHS
                      "[HKEY_LOCAL_MACHINE\\SOFTWARE\\Miru\\OpenPGM ${OPENPGM_VERSION_MAJOR}.${OPENPGM_VERSION_MINOR}.${OPENPGM_VERSION_MICRO}]"
                      NO_DEFAULT_PATH
                      )
            message(STATUS "OpenPGM x64 detected - ${OPENPGM_ROOT}")
        else()
            find_path(OPENPGM_ROOT include/pgm/pgm.h
                      PATHS
                      "[HKEY_LOCAL_MACHINE\\SOFTWARE\\Wow6432Node\\Miru\\OpenPGM ${OPENPGM_VERSION_MAJOR}.${OPENPGM_VERSION_MINOR}.${OPENPGM_VERSION_MICRO}]"
                      "[HKEY_LOCAL_MACHINE\\SOFTWARE\\Miru\\OpenPGM ${OPENPGM_VERSION_MAJOR}.${OPENPGM_VERSION_MINOR}.${OPENPGM_VERSION_MICRO}]"
                      NO_DEFAULT_PATH
                      )
            message(STATUS "OpenPGM x86 detected - ${OPENPGM_ROOT}")
        endif(CMAKE_CL_64)
        
        set(OPENPGM_INCLUDE_DIRS ${OPENPGM_ROOT}/include)
        set(OPENPGM_LIBRARY_DIRS ${OPENPGM_ROOT}/lib)
        set(OPENPGM_LIBRARIES
            optimized libpgm$-mt-${OPENPGM_VERSION_MAJOR}_${OPENPGM_VERSION_MINOR}_${OPENPGM_VERSION_MICRO}.lib
            debug libpgm$-mt-gd-${OPENPGM_VERSION_MAJOR}_${OPENPGM_VERSION_MINOR}_${OPENPGM_VERSION_MICRO}.lib)
    endif()
else()
    if(WITH_OPENPGM)
        message(FATAL_ERROR "WITH_OPENPGM not implemented")
        
        # DSO symbol visibility for openpgm
        if(HAVE_FLAG_VISIBILITY_HIDDEN)
        elseif(HAVE_FLAG_LDSCOPE_HIDDEN)
        endif()
    endif()
endif()


#-----------------------------------------------------------------------------
# force off-tree build

if(${CMAKE_CURRENT_SOURCE_DIR} STREQUAL ${CMAKE_CURRENT_BINARY_DIR})
    message(FATAL_ERROR "CMake generation is not allowed within the source directory!
            Remove the CMakeCache.txt file and try again from another folder, e.g.:

            rm CMakeCache.txt
            mkdir cmake-make
            cd cmake-make
            cmake ..
            ")
endif()

#-----------------------------------------------------------------------------
# default to Release build

if(NOT DEFINED CMAKE_BUILD_TYPE)
    set(CMAKE_BUILD_TYPE Release CACHE STRING
        "Choose the type of build, options are: None Debug Release RelWithDebInfo MinSizeRel."
        FORCE)
endif()

#-----------------------------------------------------------------------------
#default to static runtime library
if(NOT DEFINED STATIC_RUNLIB)
    option(STATIC_RUNLIB "" ON)
endif()
 
# platform specifics
  
if(MSVC)
    add_definitions(-DWIN32
                    # NB: May require tweaking for highly connected applications.
                    -DFD_SETSIZE=4096
                    -D_CRT_SECURE_NO_WARNINGS)

    #remove static/shared compiler options
    string(REGEX REPLACE "/M[TD]d?" ""  CMAKE_CXX_FLAGS                "${CMAKE_CXX_FLAGS}")
    string(REGEX REPLACE "/M[TD]d?" ""  CMAKE_CXX_FLAGS_DEBUG          "${CMAKE_CXX_FLAGS_DEBUG}")
    string(REGEX REPLACE "/M[TD]d?" ""  CMAKE_CXX_FLAGS_RELEASE        "${CMAKE_CXX_FLAGS_RELEASE}")
    string(REGEX REPLACE "/M[TD]d?" ""  CMAKE_CXX_FLAGS_RELWITHDEBINFO "${CMAKE_CXX_FLAGS_RELWITHDEBINFO}")

    # Parallel make.
    #set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} /MP")
endif()


#-----------------------------------------------------------------------------
# source files

set(cxx-sources
        address.cpp
        client.cpp
        clock.cpp
        ctx.cpp
        curve_client.cpp
        curve_server.cpp
        dealer.cpp
        devpoll.cpp
        dist.cpp
        epoll.cpp
        err.cpp
        fq.cpp
        io_object.cpp
        io_thread.cpp
        ip.cpp
        ipc_address.cpp
        ipc_connecter.cpp
        ipc_listener.cpp
        kqueue.cpp
        lb.cpp
        mailbox.cpp
        mailbox_safe.cpp
        mechanism.cpp
        metadata.cpp
        msg.cpp
        mtrie.cpp
        object.cpp
        options.cpp
        own.cpp
        null_mechanism.cpp
        pair.cpp
        pgm_receiver.cpp
        pgm_sender.cpp
        pgm_socket.cpp
        pipe.cpp
        plain_client.cpp
        plain_server.cpp
        poll.cpp
        poller_base.cpp
        precompiled.cpp
        proxy.cpp
        pub.cpp
        pull.cpp
        push.cpp
        random.cpp
        raw_encoder.cpp
        raw_decoder.cpp
        reaper.cpp
        rep.cpp
        req.cpp
        router.cpp
        select.cpp
        server.cpp
        session_base.cpp
        signaler.cpp
        socket_base.cpp
        socks.cpp
        socks_connecter.cpp
        stream.cpp
        stream_engine.cpp
        sub.cpp
        tcp.cpp
        tcp_address.cpp
        tcp_connecter.cpp
        tcp_listener.cpp
        thread.cpp
        trie.cpp
        v1_decoder.cpp
        v1_encoder.cpp
        v2_decoder.cpp
        v2_encoder.cpp
        xpub.cpp
        xsub.cpp
        zmq.cpp
        zmq_utils.cpp)

set(rc-sources version.rc)

if(MINGW)
    # Generate the right type when using -m32 or -m64
    macro(set_rc_arch rc_target)
        set(CMAKE_RC_COMPILER_INIT windres)
        enable_language(RC)
        set(CMAKE_RC_COMPILE_OBJECT
            "<CMAKE_RC_COMPILER> <FLAGS> -O coff --target=${rc_target} <DEFINES> -i <SOURCE> -o <OBJECT>")
    endmacro()

    if (NOT CMAKE_SYSTEM_PROCESSOR )
        set (CMAKE_SYSTEM_PROCESSOR ${CMAKE_HOST_SYSTEM_PROCESSOR} )
    endif ()

    if(    CMAKE_SYSTEM_PROCESSOR MATCHES "i386"
        OR CMAKE_SYSTEM_PROCESSOR MATCHES "i486"
        OR CMAKE_SYSTEM_PROCESSOR MATCHES "i586"
        OR CMAKE_SYSTEM_PROCESSOR MATCHES "i686"
        # This also happens on x86_64 systems...what a worthless variable
        OR CMAKE_SYSTEM_PROCESSOR MATCHES "x86"
        OR CMAKE_SYSTEM_PROCESSOR MATCHES "x86_64"
        OR CMAKE_SYSTEM_PROCESSOR MATCHES "amd64")

        if(CMAKE_SIZEOF_VOID_P EQUAL 8)
            set_rc_arch("pe-x86-64")
        else()
            set_rc_arch("pe-i386")
        endif()
    endif()
endif()

include_directories(include ${CMAKE_CURRENT_BINARY_DIR})
set(public_headers include/zmq.h
                   include/zmq_utils.h)

set(readme-docs AUTHORS
                COPYING
                COPYING.LESSER
                MAINTAINERS
                NEWS)

#-----------------------------------------------------------------------------
# optional modules

if(WITH_OPENPGM)
    add_definitions(-DZMQ_HAVE_OPENPGM)
    include_directories(${OPENPGM_INCLUDE_DIRS})
    link_directories(${OPENPGM_LIBRARY_DIRS})
    set(OPTIONAL_LIBRARIES ${OPENPGM_LIBRARIES})
endif(WITH_OPENPGM)

#-----------------------------------------------------------------------------
# source generators

foreach(source ${cxx-sources})
    list(APPEND sources ${CMAKE_CURRENT_SOURCE_DIR}/src/${source})
endforeach()

foreach(source ${TWEETNACL_SOURCES})
    list(APPEND sources ${CMAKE_CURRENT_SOURCE_DIR}/${source})
endforeach()

foreach(source ${rc-sources})
    list(APPEND sources ${CMAKE_CURRENT_BINARY_DIR}/${source})
    configure_file(${CMAKE_CURRENT_SOURCE_DIR}/src/${source}.in ${CMAKE_CURRENT_BINARY_DIR}/${source})
endforeach()

configure_file(${CMAKE_CURRENT_SOURCE_DIR}/builds/cmake/platform.hpp.in ${CMAKE_CURRENT_BINARY_DIR}/platform.hpp)
list(APPEND sources ${CMAKE_CURRENT_BINARY_DIR}/platform.hpp)

configure_file(${CMAKE_CURRENT_SOURCE_DIR}/src/libzmq.pc.cmake.in ${CMAKE_CURRENT_BINARY_DIR}/libzmq.pc @ONLY)
set(zmq-pkgconfig ${CMAKE_CURRENT_BINARY_DIR}/libzmq.pc)


install(FILES ${CMAKE_CURRENT_BINARY_DIR}/libzmq.pc DESTINATION lib/pkgconfig)


if(MSVC)
    if(CMAKE_CL_64)
        set(nsis-template ${CMAKE_CURRENT_SOURCE_DIR}/builds/cmake/NSIS.template64.in)
    else()
        set(nsis-template ${CMAKE_CURRENT_SOURCE_DIR}/builds/cmake/NSIS.template32.in)
    endif()

    add_custom_command(OUTPUT ${CMAKE_CURRENT_BINARY_DIR}/NSIS.template.in
                       COMMAND ${CMAKE_COMMAND}
                       ARGS -E
                       copy
                       ${nsis-template}
                       ${CMAKE_CURRENT_BINARY_DIR}/NSIS.template.in
                       DEPENDS ${nsis-template})
endif()

file(MAKE_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}/doc)
file(GLOB docs RELATIVE ${CMAKE_CURRENT_BINARY_DIR}/ "${CMAKE_CURRENT_SOURCE_DIR}/doc/*.txt")
set(html-docs)
foreach(txt ${docs})
    string(REGEX REPLACE ".*/(.*)\\.txt" "\\1.html" html ${txt})
    set(src ${txt})
    set(dst doc/${html})
    add_custom_command(OUTPUT  ${dst}
                       COMMAND ${PYTHON_EXECUTABLE}
                       ARGS    -x
                       ${ASCIIDOC_EXECUTABLE}
                       -d manpage
                       -b xhtml11
                       -f ${CMAKE_CURRENT_SOURCE_DIR}/doc/asciidoc.conf
                       -azmq_version=${ZMQ_VERSION}
                       -o ${dst}
                       ${src}
                       DEPENDS ${CMAKE_CURRENT_BINARY_DIR}/${src}
                       WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
                       COMMENT "Generating ${html}")
    if(WITH_DOC)
        list(APPEND html-docs ${CMAKE_CURRENT_BINARY_DIR}/${dst})
    endif()
endforeach()

#set up additional link libraries
set(MORE_LIBRARIES ${SODIUM_LIBRARY} ${CMAKE_THREAD_LIBS_INIT} "${OPTIONAL_LIBRARIES}")
if(HAVE_WS2_32)
    set(MORE_LIBRARIES "${MORE_LIBRARIES}" ws2_32)
elseif(HAVE_WS2)
    set(MORE_LIBRARIES "${MORE_LIBRARIES}" ws2)
endif()

if(HAVE_RPCRT4)
    set(MORE_LIBRARIES "${MORE_LIBRARIES}" rpcrt4)
endif()

if(HAVE_IPHLAPI)
    set(MORE_LIBRARIES "${MORE_LIBRARIES}" iphlpapi)
endif()

if(RT_LIBRARY)
    set(MORE_LIBRARIES "${MORE_LIBRARIES}" "${RT_LIBRARY}")
endif()


#-----------------------------------------------------------------------------
# output

if(STATIC_RUNLIB)
    #static runtime library
    set(static_flag_debug "s")
    set(static_flag_release "s-")
    if (MSVC)
        set(STATIC_COMPILE_OPTIONS "/MT$<$<CONFIG:Debug>:d>")
    else()
        #set other compiler options here
    endif()    
else()
    #dynamic runtime library
    set(static_flag_debug "")
    set(static_flag_release "")
    if (MSVC)
        set(STATIC_COMPILE_OPTIONS "/MD$<$<CONFIG:Debug>:d>")
    else()
        #set other compiler options here
    endif()
endif()

if(MSVC)
    #build shared lib
    add_library(libzmq SHARED ${sources} ${public_headers} ${html-docs} ${readme-docs} ${CMAKE_CURRENT_BINARY_DIR}/NSIS.template.in)
    set_target_properties(libzmq PROPERTIES
                          PUBLIC_HEADER "${public_headers}"
                          RELEASE_POSTFIX "-mt-${ZMQ_VERSION_MAJOR}_${ZMQ_VERSION_MINOR}_${ZMQ_VERSION_PATCH}"
                          RELWITHDEBINFO_POSTFIX "-mt-${ZMQ_VERSION_MAJOR}_${ZMQ_VERSION_MINOR}_${ZMQ_VERSION_PATCH}"
                          DEBUG_POSTFIX "-mt-gd-${ZMQ_VERSION_MAJOR}_${ZMQ_VERSION_MINOR}_${ZMQ_VERSION_PATCH}"
                          RUNTIME_OUTPUT_DIRECTORY "${CMAKE_CURRENT_BINARY_DIR}/lib"
                          ARCHIVE_OUTPUT_DIRECTORY "${CMAKE_CURRENT_BINARY_DIR}/lib"
                          COMPILE_OPTIONS "/D DLL_EXPORT"
                          OUTPUT_NAME "zmq")
    target_compile_options(libzmq PRIVATE "/MD$<$<CONFIG:Debug>:d>")
    target_link_libraries(libzmq "${MORE_LIBRARIES}")
        
    #build static lib - which is prefixed with lib to distinguish it from shared lib   
    add_library(libzmq-static STATIC ${sources})
    set_target_properties(libzmq-static PROPERTIES
                          PUBLIC_HEADER "${public_headers}"
                          RELEASE_POSTFIX "-mt-${static_flag_release}${ZMQ_VERSION_MAJOR}_${ZMQ_VERSION_MINOR}_${ZMQ_VERSION_PATCH}"
                          RELWITHDEBINFO_POSTFIX "-mt-${static_flag_release}${ZMQ_VERSION_MAJOR}_${ZMQ_VERSION_MINOR}_${ZMQ_VERSION_PATCH}"
                          DEBUG_POSTFIX "-mt-${static_flag_debug}gd-${ZMQ_VERSION_MAJOR}_${ZMQ_VERSION_MINOR}_${ZMQ_VERSION_PATCH}"
                          ARCHIVE_OUTPUT_DIRECTORY "${CMAKE_CURRENT_BINARY_DIR}/lib"
                          COMPILE_OPTIONS "/D ZMQ_STATIC"
                          OUTPUT_NAME "libzmq")
    target_compile_options(libzmq-static PRIVATE "${STATIC_COMPILE_OPTIONS}")
        
else()
    #build shared lib   
    add_library(libzmq SHARED ${sources} ${public_headers} ${html-docs} ${readme-docs} ${zmq-pkgconfig})
    set_target_properties(libzmq PROPERTIES
                         PUBLIC_HEADER "${public_headers}"
                         RELEASE_POSTFIX "-mt-${ZMQ_VERSION_MAJOR}_${ZMQ_VERSION_MINOR}_${ZMQ_VERSION_PATCH}"
                         RELWITHDEBINFO_POSTFIX "-mt-${ZMQ_VERSION_MAJOR}_${ZMQ_VERSION_MINOR}_${ZMQ_VERSION_PATCH}"
                         DEBUG_POSTFIX "-mt-gd-${ZMQ_VERSION_MAJOR}_${ZMQ_VERSION_MINOR}_${ZMQ_VERSION_PATCH}"
                         LIBRARY_OUTPUT_DIRECTORY "${CMAKE_CURRENT_BINARY_DIR}/lib"
                         ARCHIVE_OUTPUT_DIRECTORY "${CMAKE_CURRENT_BINARY_DIR}/lib"
                         COMPILE_OPTIONS "/D DLL_EXPORT"
                         OUTPUT_NAME "zmq")
                         
    #build static lib - which is prefixed with lib to distinguish it from shared lib                      
    target_link_libraries(libzmq "${MORE_LIBRARIES}")
    add_library(libzmq-static STATIC ${sources} ${public_headers} ${html-docs} ${readme-docs} ${zmq-pkgconfig})
    set_target_properties(libzmq-static PROPERTIES
                          PUBLIC_HEADER "${public_headers}"
                          RELEASE_POSTFIX "-mt-${static_flag_release}${ZMQ_VERSION_MAJOR}_${ZMQ_VERSION_MINOR}_${ZMQ_VERSION_PATCH}"
                          RELWITHDEBINFO_POSTFIX "-mt-${static_flag_release}${ZMQ_VERSION_MAJOR}_${ZMQ_VERSION_MINOR}_${ZMQ_VERSION_PATCH}"
                          DEBUG_POSTFIX "-mt-${static_flag_debug}gd-${ZMQ_VERSION_MAJOR}_${ZMQ_VERSION_MINOR}_${ZMQ_VERSION_PATCH}"
                          ARCHIVE_OUTPUT_DIRECTORY "${CMAKE_CURRENT_BINARY_DIR}/lib"
                          COMPILE_OPTIONS "/D ZMQ_STATIC"
                          OUTPUT_NAME "libzmq")
    target_compile_options(libzmq-static PRIVATE "${STATIC_COMPILE_OPTIONS}")
endif()

set(perf-tools local_lat
               remote_lat
               local_thr
               remote_thr
               inproc_lat
               inproc_thr)


foreach(perf-tool ${perf-tools})
    add_executable("${perf-tool}" "perf/${perf-tool}.cpp")
    if(MSVC)
        set_target_properties("${perf-tool}" PROPERTIES
                              RUNTIME_OUTPUT_DIRECTORY "${CMAKE_CURRENT_BINARY_DIR}/bin"
                              COMPILE_OPTIONS "/D ZMQ_STATIC")
    else()
        set_target_properties("${perf-tool}" PROPERTIES
                              RUNTIME_OUTPUT_DIRECTORY "${CMAKE_CURRENT_BINARY_DIR}/bin"
                              COMPILE_OPTIONS "/D ZMQ_STATIC")
    endif() 
    target_compile_options("${perf-tool}" PRIVATE "${STATIC_COMPILE_OPTIONS}")
    target_link_libraries("${perf-tool}" libzmq-static "${MORE_LIBRARIES}")
        
    install(TARGETS "${perf-tool}"
            RUNTIME DESTINATION bin
            COMPONENT PerfTools)
endforeach()


#-----------------------------------------------------------------------------
# tests
if (NOT DEFINED ZMQ_BUILD_TESTS)
    OPTION(ZMQ_BUILD_TESTS "" ON)
endif()

if(ZMQ_BUILD_TESTS)
  enable_testing() # Enable testing only works in root scope
  ADD_SUBDIRECTORY(tests)
endif()

#-----------------------------------------------------------------------------
# installer

if(MSVC)
    install(TARGETS libzmq libzmq-static EXPORT ZMQ-TARGETS
            RUNTIME DESTINATION lib
            ARCHIVE DESTINATION lib
            PUBLIC_HEADER DESTINATION include
            COMPONENT SDK)
else()
    install(TARGETS libzmq libzmq-static EXPORT ZMQ-TARGETS
            LIBRARY DESTINATION lib
            ARCHIVE DESTINATION lib
            FRAMEWORK DESTINATION "Library/Frameworks"
            PUBLIC_HEADER DESTINATION include)
endif()

#desire would be to export these with below but broken
#install(EXPORT ZMQ-TARGETS DESTINATION lib)

file(GLOB private_headers "${CMAKE_CURRENT_SOURCE_DIR}/src/*.hpp")
install(FILES "${sources}" "${private_headers}" DESTINATION src/zmq
        COMPONENT SourceCode)

foreach(readme ${readme-docs})
    configure_file(${CMAKE_CURRENT_SOURCE_DIR}/${readme} ${CMAKE_CURRENT_BINARY_DIR}/${readme}.txt)

    if(MSVC)
        install(FILES ${CMAKE_CURRENT_BINARY_DIR}/${readme}.txt DESTINATION .)
    else()
        install(FILES ${CMAKE_CURRENT_BINARY_DIR}/${readme}.txt DESTINATION etc/zmq)
    endif()
endforeach()

if(WITH_DOC)
    install(FILES ${html-docs} DESTINATION doc/zmq COMPONENT RefGuide)
endif()
